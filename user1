Option Explicit

Dim Pos(10, 10) As String               'ポジション名 (シートに何人いるか, ボタンから数えて何番目か)
Dim Pos_Of_Player(10, 300) As String    'ポジション (プレイヤーNo, 何ハンド目か)
Dim G_End As Integer                    '何ハンド目でゲームが終わるか
Dim On_Seat(300) As Integer             'シートに何人いるか (何ハンド目か)
Dim BTN(300) As Integer                 'ボタンが何番目のプレイヤーか (何ハンド目か)
Dim i, k, n, p As Integer               '汎用の変数

Sub Discript_Position()
    
'再計算・画面更新オフ
Application.ScreenUpdating = False
Application.Calculation = xlCalculationManual

'各ワークシートの定義
Dim TH As Worksheet: Set TH = Worksheets("T_Hand")
Dim TP As Worksheet: Set TP = Worksheets("T_Position")

'ポジションの定義
Dim_Position

'何ハンド目でゲームが終わるか、及び各ハンドの人数を調べる
Calc_Game_End

'最初のボタンプレイヤー取得（参照先はあとで別のとこにするかも）
BTN(1) = TP.Range("C12").Value

TH.Range("M4:V303").ClearContents

For n = 1 To G_End

    Select Case n
    
        '1ハンド目。スタートボタンから順番にポジションを配る
        Case 1
            
            k = BTN(1)
            For i = 1 To On_Seat(1)
            
                Pos_Of_Player(k, 1) = Pos(On_Seat(1), i)
                k = k + 1
                If k > On_Seat(1) Then k = 1
                
            Next i
            
            For i = 1 To On_Seat(1) '<----シートに記述（あとで一括にまとめる方がいい）
                TH.Cells(4, i + 12).Value = Pos_Of_Player(i, 1)
            Next i
            
        '2ハンド目以降
        Case Else
            
            'ボタンを1つ動かす
            BTN(n) = BTN(n - 1) + 1
            If BTN(n) > On_Seat(1) Then BTN(n) = 1
               
            'シートオープンしている人は飛ばす。前ハンドSB(SBd)の場合デッドボタンとする
            Do
                If TH.Cells(n + 3, BTN(n) + 2) <> "" Then Exit Do
                If Pos_Of_Player(BTN(n), n - 1) Like "SB*" Then Exit Do
                BTN(n) = BTN(n) + 1
                If BTN(n) > On_Seat(1) Then BTN(n) = 1
            Loop
                
            k = BTN(n)
            p = 1
            
            'ボタンから順番にポジションを配る
            For i = 1 To On_Seat(1)
                        
                'シートオープンしている人の処理(ハンドを持っていない場合、いないと見なす)
                If TH.Cells(n + 3, k + 2) = "" Then
                
                    '前ハンドBBだった場合、1Bigとなる。当ハンドのシート数を暫定で1つ増やしておく
                    '1 BigのときのSBを「SBd」と表記する
                    If Pos_Of_Player(k, n - 1) = "BB" Then
                        On_Seat(n) = On_Seat(n) + 1
                        Pos_Of_Player(k, n) = Pos(On_Seat(n), p) & "d"
                        p = p + 1
                    
                    '前ハンドSB(SBd)だった場合、デッドボタンとなる。当ハンドのシート数を暫定で1つ増やしておく
                    'デッドボタンを「BTNd」と表記する
                    ElseIf Pos_Of_Player(k, n - 1) Like "SB*" Then
                        On_Seat(n) = On_Seat(n) + 1
                        Pos_Of_Player(k, n) = Pos(On_Seat(n), p) & "d"
                        p = p + 1
                        
                    'その他、何も無ければ空欄とする
                    Else
                        Pos_Of_Player(k, n) = ""
                    End If
                    
                'シートにいる人には、順番にポジションを配る
                Else
                    Pos_Of_Player(k, n) = Pos(On_Seat(n), p)
                    p = p + 1
                End If
                        
                k = k + 1
                If k > On_Seat(1) Then k = 1
                    
            Next i
            
            For i = 1 To On_Seat(1) 'シートに記述（あとで一括にまとめる方がいい）
                TH.Cells(n + 3, i + 12).Value = Pos_Of_Player(i, n)
            Next i
                   
    End Select

Next n

'再計算・画面更新オン
Application.ScreenUpdating = True
Application.Calculation = xlCalculationAutomatic
Application.Calculate

End Sub

Sub Dim_Position()

Dim X, Y As Integer
Dim TP As Worksheet: Set TP = Worksheets("T_Position")

For X = 2 To 10
    For Y = 1 To X
        Pos(X, Y) = TP.Cells(X, Y + 1).Value
    Next Y
Next X

End Sub

Sub Calc_Game_End()

Dim TH As Worksheet: Set TH = Worksheets("T_Hand")

For n = 1 To 3000
    On_Seat(n) = TH.Cells(n + 3, 23).Value
    If On_Seat(n) = 0 Then
        G_End = n - 1: Exit For
    End If
Next n

End Sub
